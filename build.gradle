plugins {
    id 'idea'
    id 'java-library'
    id "maven-publish"
    id 'org.springframework.boot' version '3.1.4'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'jacoco'
    id "org.sonarqube" version "4.4.1.3373"
    id 'org.owasp.dependencycheck' version '8.4.0'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

Properties env = new Properties()
File subPropertiesFile = new File("$projectDir/env.properties")
subPropertiesFile.withInputStream {
    env.load(it)
}

allprojects {
    apply plugin: 'org.sonarqube'

    group = env.APPLICATION_GROUP
    version = env.APPLICATION_VERSION

    def javaVersion = JavaVersion.toVersion(env.JAVA_VERSION)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    compileJava {
        options.compilerArgs += [
                '-Amapstruct.defaultComponentModel=spring'
        ]
        options.encoding = env.ENCODING
    }

    java {
        sourceCompatibility = env.getProperty("JAVA_VERSION")
    }

    repositories {
        mavenCentral()
    }


    sonar {
        properties {
            property "sonar.projectKey", "BillyBolton_springharvest"
            property "sonar.organization", "billybolton"
            property "sonar.host.url", "https://sonarcloud.io"
        }
    }
}

tasks.named('sonar').configure {
    dependsOn jacocoTestReport
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java-library'
    apply plugin: "maven-publish"
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'
    apply plugin: 'org.sonarqube'
    apply plugin: 'org.owasp.dependencycheck'

    jacocoTestReport {
        reports {
            xml.required.set(true)
            html.required.set(true)
        }
        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        "**/models/**",
                        "**/constants/**",
                        "**/mappers/**",
                        "**/config/**",
                        "**/errors/**",
                        "**/*Repository.*",
                        "**/*Mapper*.*",
                        "**/*Application.*"
                ])
            }))
        }
    }
    test.finalizedBy jacocoTestReport

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/billybolton/springharvest")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                    password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                from(components.java)
            }
        }
    }

    ext {
        set('testcontainersVersion', "1.17.6")
        APACHE_COMMONS_COLLECTIONS_VERSION = "4.4"
        APACHE_COMMONS_COMPRESS_VERSION = "1.24.0"
        APACHE_COMMONS_LANG_VERSION = "3.13.0"
        APACHE_TOMCAT_VERSION = "10.1.13"
        HAMCREST_VERSION = "1.3"
        HIBERNATE_JPA_MODELGEN_VERSION = "5.6.15.Final"
        LOMBOK_MAPSTRUCT_VERSION = "0.2.0"
        MAPSTRUCT_VERSION = "1.5.5.Final"
        MODELMAPPER_VERSION = "3.1.1"
        RESTASSURED_VERSION = "5.3.2"
        SNAKE_YAML_VERSION = "2.2"
        SPRING_BATCH_EXCEL_VERSION = "0.1.1"
        SPRINGDOC_VERSION = "2.2.0"
        TESTCONTAINERS_VERSION = "1.19.0"
    }

    dependencyManagement {
        imports {
            mavenBom "org.testcontainers:testcontainers-bom:$TESTCONTAINERS_VERSION"
        }
    }

    dependencies {
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        api 'org.springframework.boot:spring-boot-starter-actuator'
        api 'org.springframework.boot:spring-boot-starter-data-jpa'
        api 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        api 'org.springframework.session:spring-session-core'

        api "org.mapstruct:mapstruct:$MAPSTRUCT_VERSION"
        compileOnly 'org.projectlombok:lombok'

        //  API documentation
        api "org.springdoc:springdoc-openapi-starter-webmvc-ui:$SPRINGDOC_VERSION"
        api "org.springdoc:springdoc-openapi-starter-webmvc-api:$SPRINGDOC_VERSION"

        // ANNOTATIONS:
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        annotationProcessor 'org.projectlombok:lombok'
        annotationProcessor "org.mapstruct:mapstruct-processor:$MAPSTRUCT_VERSION"
        // required for binding mapstruct and lombok together
        annotationProcessor "org.projectlombok:lombok-mapstruct-binding:$LOMBOK_MAPSTRUCT_VERSION"

        // TESTING:
        testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        testAnnotationProcessor "org.mapstruct:mapstruct-processor:$MAPSTRUCT_VERSION"
        testImplementation "io.rest-assured:spring-mock-mvc:$RESTASSURED_VERSION"
        testImplementation "org.hamcrest:hamcrest-all:$HAMCREST_VERSION"
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.testcontainers:junit-jupiter'

        // SEARCH
        // For easier Specification Search
        annotationProcessor "org.hibernate:hibernate-jpamodelgen-jakarta:$HIBERNATE_JPA_MODELGEN_VERSION"
        implementation "org.hibernate:hibernate-jpamodelgen-jakarta:$HIBERNATE_JPA_MODELGEN_VERSION"

        // Batch Processing
        implementation 'org.springframework.boot:spring-boot-starter-batch'
        implementation "org.springframework.batch.extensions:spring-batch-excel:$SPRING_BATCH_EXCEL_VERSION"

        implementation "org.modelmapper:modelmapper:$MODELMAPPER_VERSION"

        implementation "org.apache.commons:commons-collections4:$APACHE_COMMONS_COLLECTIONS_VERSION"
        implementation "org.apache.commons:commons-lang3:$APACHE_COMMONS_LANG_VERSION"

        // VULNERABILITY FIXES
        api "org.apache.tomcat.embed:tomcat-embed-core:$APACHE_TOMCAT_VERSION"
        api "org.yaml:snakeyaml:$SNAKE_YAML_VERSION"
        api "org.apache.commons:commons-compress:$APACHE_COMMONS_COMPRESS_VERSION"
        testAnnotationProcessor "org.apache.commons:commons-compress:$APACHE_COMMONS_COMPRESS_VERSION"
    }
}

wrapper {
    gradleVersion = "8.3"
    distributionType = Wrapper.DistributionType.ALL
}